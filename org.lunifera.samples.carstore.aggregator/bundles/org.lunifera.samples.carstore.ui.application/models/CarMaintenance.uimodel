package org.lunifera.samples.carstore.views.general

ideview CarMaintenance {
	sharedStateGroup "org.lunifera.samples.carstore.general" exposedActions {
		deleteEntity id org.lunifera.actions.delete
		saveEntity id org.lunifera.actions.save
		newEntity id org.lunifera.actions.addNew
	}

	datasource main:org.lunifera.samples.carstore.dtos.general.CarDto
	eventTopic "org/lunifera/samples/carstore/CarDto/selection"

	verticalLayout {
		horizontalLayout {
			form Detail_left {
				textfield number
				numericField axes
				decimalField weightAmount

				bind main.number <--> [this.number].value
				bind main.axes <--> [this.axes].value
				bind main.weight.amount <--> [this.weightAmount].value
			}
			align top-left form Detail_right {
				textfield description
				checkbox(readonly) dirty
				referenceField weightUom {
					type org.lunifera.samples.carstore.dtos.general.UnitOfMeasureDto
					captionField description refSource org.lunifera.samples.carstore.dtos.general.CarDto:weight
				}

				bind main.description <--> [this.description].value
				bind main.dirty --> [this.dirty].value
				bind main.weight.uom <--> [this.weightUom].value
			}
			align top-left
		}
		table configDetails {
			type org.lunifera.samples.carstore.dtos.general.ConfigDetailDefinitionDto

			columns {
				column number
				column description
				column price.amount
				column price.currency.iso3Code
			}

			bind main.configDetails <--> [this].collection
		}
	}

	bind main.dirty --> [CarMaintenance.saveEntity].enabled
	bind [CarMaintenance.saveEntity].onClick --> sendEvent("org/lunifera/samples/carstore/CarDto/table/refresh")
	bind [CarMaintenance.deleteEntity].onClick --> sendEvent("org/lunifera/samples/carstore/CarDto/table/refresh")
	bind [CarMaintenance.deleteEntity].onClick --> newInstance main
	bind [CarMaintenance.newEntity].onClick --> newInstance main
}
