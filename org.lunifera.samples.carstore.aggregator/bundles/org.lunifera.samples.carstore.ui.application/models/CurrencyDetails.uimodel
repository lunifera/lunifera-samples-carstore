package org.lunifera.samples.carstore.views.general

ideview CurrencyDetails {
	
	sharedStateGroup "org.lunifera.samples.carstore.general"

	exposedActions {
		reloadEntity id org.lunifera.actions.load 
		deleteEntity id org.lunifera.actions.delete
		saveEntity id org.lunifera.actions.save
		newEntity id org.lunifera.actions.addNew checkDirty
		validateEntity id org.lunifera.actions.validate
	}

	category org.lunifera.vaaclipse.addons.application.partstack.main

	datasource main:org.lunifera.samples.carstore.dtos.general.CurrencyDto eventTopic "org/lunifera/samples/carstore/CurrencyDto/selection"

	horizontalLayout Detail {
		autowire source main
	} align fill-fill 
	 visibility CurrencyActions {
		fireOn main as main
		fireOn main.dirty as dirty
		ui [this.reloadEntity] as reloadAction
		ui [this.deleteEntity] as deleteAction
		ui [this.saveEntity] as saveAction
		ui [this.validateEntity] as validateAction
		ui [this.Detail] as mainLayout
		{
			mainLayout.enabled = true
			reloadAction.enabled = false
			deleteAction.enabled = false
			saveAction.enabled = false
			validateAction.enabled = false
			if(main != null){
				if(dirty){
					reloadAction.enabled = true
					saveAction.enabled = true
				}
				deleteAction.enabled = true
				validateAction.enabled = true
			} else {
				mainLayout.enabled = false
			}
		}
	}

	bind [this.saveEntity].onExecuted --> sendEvent("org/lunifera/samples/carstore/CurrencyDto/table/refresh")
	bind [this.deleteEntity].onExecuted --> sendEvent("org/lunifera/samples/carstore/CurrencyDto/table/refresh")
	bind [this.deleteEntity].onExecuted --> [this.newEntity].click
	bind [this.reloadEntity].onExecuted --> sendEvent("org/lunifera/samples/carstore/CurrencyDto/table/refresh")

	// new cars
	bind [this.newEntity].onExecuted --> newInstance at main
}
