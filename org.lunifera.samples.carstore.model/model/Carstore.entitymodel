package org.lunifera.samples.carstore {

	datatype LDate dateType date;

	mapped superclass BaseUUID {
		uuid String id;
	}

	entity UnitOfMeasure extends BaseUUID {
		var String number;
		var String name;
		var SITypeEnum siType;
		var UOMFamilyEnum family;
	}
	
	entity PaymentTerm extends BaseUUID {
		var String number;
		var String description;
	}
	
	entity Currency extends BaseUUID {
		var String number;
		var String description;
		var String iso3Code;
	}
	
	entity Customer extends BaseUUID {
		var String number;
		var String name;
		var Address address;
		ref PaymentTerm paymentTerm;
		ref SalesOrder[0..*] orders opposite customer;
	}
	
	entity SalesOrder extends BaseUUID {
		var String number;
		ref Currency currency;
		ref Customer[1] customer opposite orders;
		ref cascade SODetail[1..*] soDetails opposite order;
	}

	entity SODetail extends BaseUUID {
		ref SalesOrder order opposite soDetails;
		ref Item item;
	}
	
	abstract entity Item extends BaseUUID {
		var String number;
		var String description;
		ref UnitOfMeasure uom;
	}
	
	entity Accessory extends Item {
	}
	
	abstract entity Car extends Item {
		var int axes;
		var Weight weight;
		var Price price;
		ref ConfigDetail[0..*] configDetails opposite car;
	}	
	
	entity ConfigDetail extends BaseUUID {
		ref Car[1] car opposite configDetails;
		var String number;
		var String description;
		var Price price;		
	}
	
	entity Pickup extends Car {
		var Length loadingAreaWidth;
		var Length loadingAreaLength;
	}
	
	entity Convertible extends Car {
		var RoofType roofType;
		var String color;
	}
	
	bean Weight {
		var int amount;
		var SITypeEnum siType;
	}
	
	bean Price {
		var int amount;
		var String currency;
	}
	
	bean Length {
		var int amount;
		var SITypeEnum siType;
	}

	bean Address {
		var String street;
		var String postalcode;
	}
	
	enum UOMFamilyEnum {
		WEIGHT, LENGTH, QTY, TIME
	}
	
	enum SITypeEnum {
		km, m, cm, mm, 
		h, min, sec, 
		t, kg, g
	}
	
	enum RoofType {
		SOLID, FOLDABLE, NONE
	}
	
}